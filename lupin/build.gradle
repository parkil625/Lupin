plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'org.sonarqube' version '4.4.1.3373'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'info.solidsoft.pitest' version '1.15.0'
}

// REST Docs 설정
ext {
	snippetsDir = file('build/generated-snippets')
}

group = 'com.final'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Database
	runtimeOnly 'com.mysql:mysql-connector-j'

	// Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// AWS S3 (Spring Cloud AWS 3.x)
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.1'

	// Google OAuth2
	implementation 'com.google.api-client:google-api-client:2.2.0'

	// Dotenv for environment variables
	implementation 'me.paulschwarz:spring-dotenv:4.0.0'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'

	// Testcontainers
	testImplementation 'org.testcontainers:testcontainers:1.19.3'
	testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
	testImplementation 'org.testcontainers:mysql:1.19.3'
	testImplementation 'org.testcontainers:toxiproxy:1.19.3'

	// PITest for mutation testing
	testImplementation 'org.pitest:pitest-junit5-plugin:1.2.1'

	// Redis 추가: 고송 토큰 저장소
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Spring REST Docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

tasks.named('test') {
	useJUnitPlatform()
	outputs.dir snippetsDir
	finalizedBy jacocoTestReport
}

// Asciidoctor 설정 (REST Docs 문서 생성)
tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

// 문서를 static 폴더로 복사
tasks.register('copyDocs', Copy) {
	dependsOn asciidoctor
	from "${asciidoctor.outputDir}"
	into 'src/main/resources/static/docs'
}

// JaCoCo 설정
jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.30 // 30% 최소 커버리지
			}
		}
	}
}

// SonarQube 설정
sonarqube {
	properties {
		property "sonar.projectKey", "parkil625_Lupin"
		property "sonar.organization", "parkil625"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
	}
}

// PITest (Mutation Testing) 설정
pitest {
	junit5PluginVersion = '1.2.1'
	targetClasses = ['com.example.demo.service.*']  // 핵심 서비스 로직만 타겟
	targetTests = ['com.example.demo.service.*Test']
	threads = 4
	outputFormats = ['HTML', 'XML']
	timestampedReports = false
	mutationThreshold = 60  // 60% 이상 돌연변이 사멸 목표
	coverageThreshold = 60
}
